/*************************************************************************
 *
 *  This file is part of ACT standard library
 *
 *  Copyright (c) 2024 Fabian Posch
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 *
 **************************************************************************
 */

namespace sim {

// external C functions for file interaction
namespace file_private {
export function openr (int<32> file_id) : int<32>;
export function read (int<32> reader_id) : int<64>;
export function eof (int<32> reader_id) : bool;
export function closer (int<32> reader_id) : bool;
}

/**
 * Deprecated check sink from old simulation library
 *
 * This should not be used in new designs and will be removed
 * in next version!
 *
 */
export template<pint D_WIDTH; pint F_ID; pbool LOOP>
defproc check_sink (chan?(int<D_WIDTH>) I)
{
    int<D_WIDTH> x, y;
    int reader_id;
    bool dummy;

    chp {
        warn ("check_sink is deprecated and will be removed in a future version!");

        *[
            // open the known good output file for reading
            reader_id := file_private::openr (F_ID);

            *[
                // receive the input value
                I?x;
                y := file_private::read(reader_id);

                // compare the values
                [ x = y -> 
                    skip
                [] else -> 
                    log ("ASSERTION failed, value mismatch; expected: ", y, "%x (0x", y, "); got: ", x, "%x (0x", x, ")")
                ]

	        <- ~file_private::eof(reader_id) ];

            dummy := file_private::closer(reader_id)

        <- LOOP ];
        log ("Sink file #", F_ID, " ends.")
    }
}

export namespace scoreboard {

/*
 * Generic scoreboard
 *
 * Use this if your checks are more intricate and you only
 * wish to use standardized logging
 *
 * Features:
 * - Output token logging
 *
 * Assumes:
 * - Valid channel receives true token if test succeeded
 *
 * Parameters:
 * - D_WIDTH: Data output bus width
 * - OUT_CHANNELS: Number of output channels coming from the model / design
 * - SB_ID: ID of the scoreboard (used in log output)
 * - VERBOSE_TESTING: If false, only failed tests are logged
 *
 */
export template <pint D_WIDTH, OUT_CHANNELS, SB_ID; pbool VERBOSE_TESTING>
defproc generic (chan?(int<D_WIDTH>) OUT_M[OUT_CHANNELS], OUT_D[OUT_CHANNELS]; chan?(bool) SUCCESS)
{
    int<D_WIDTH> output_m[OUT_CHANNELS], output_d[OUT_CHANNELS];
    bool success;
    int num;

    chp {
        
        num := 0;

        *[
            // receive the data
            SUCCESS?success,
            (, i : OUT_CHANNELS : OUT_M[i]?output_m[i]),
            (, j : OUT_CHANNELS : OUT_D[j]?output_d[j]);

            // print the output
            [ success -> (
                
                // only print successful tests if verbose testing is enabled
                [ VERBOSE_TESTING -> (
                    log_st ("");
                    log_p ("Scoreboard ", SB_ID, ": TEST SUCCESS (", num, "); outputs {");
                    (; i : OUT_CHANNELS : log_p (i, ": ", output_d[i], "%x (0x", output_d[i], "); "));
                    log_p ("}");
                    log_nl ("")
                ) 
                []  else -> (
                    skip
                )]
            )
            []  else -> (
                log_st ("");
                log_p ("Scoreboard ", SB_ID, ": TEST FAILED (", num, "); outputs {");
                (; i : OUT_CHANNELS : log_p (i, ": expected ", output_m[i], "%x (0x", output_m[i], "), got ", output_d[i], "%x (0x", output_d[i], "); "));
                log_p ("}");
                log_nl ("")
            )];
            num := num + 1
        ]

    }
}

/*
 * Lockstep scoreboard
 *
 * Use this for a simple linear pipeline
 *
 * Features:
 * - Input token logging
 * - Input token to output token matching
 * - Output token comparison
 * - Token counter
 *
 * Assumes:
 * - Input token corresponds directly to output token
 * - Same number of tokens on all channels
 * - Tokens on all channels in same order
 *
 * Parameters:
 * - D_WIDTH: Data output bus width
 * - IN_CHANNELS: Number of input channels going to the model / design
 * - OUT_CHANNELS: Number of output channels coming from the model / design
 * - SB_ID: ID of the scoreboard (used in log output)
 * - VERBOSE_TESTING: If false, only failed tests are logged
 *
 */
export template <pint D_WIDTH, IN_CHANNELS, OUT_CHANNELS, SB_ID; pbool VERBOSE_TESTING>
defproc lockstep (chan?(int<D_WIDTH>) IN[IN_CHANNELS], OUT_M[OUT_CHANNELS], OUT_D[OUT_CHANNELS])
{
    int<D_WIDTH> input[IN_CHANNELS], output_m[OUT_CHANNELS], output_d[OUT_CHANNELS];
    bool not_failed;
    int num;

    chp {
        
        num := 0;

        *[
            // receive the data
            (, i : IN_CHANNELS : IN[i]?input[i]),
            (, i : OUT_CHANNELS : OUT_M[i]?output_m[i]),
            (, j : OUT_CHANNELS : OUT_D[j]?output_d[j]);

            // check the results
            not_failed+;
            (; i : OUT_CHANNELS : [not_failed -> not_failed := output_m[i] = output_d[i] [] else -> skip]);

            // print the output
            [ not_failed -> (
                // only print successful tests if verbose testing is enabled
                [ VERBOSE_TESTING -> (
                    log_st ("");
                    log_p ("Scoreboard ", SB_ID, ": TEST SUCCESS (", num, "); inputs {");
                    (; i : IN_CHANNELS : log_p (i, ": ", input[i], "%x (0x", input[i], "); "));
                    log_p ("}, outputs {");
                    (; i : OUT_CHANNELS : log_p (i, ": ", output_d[i], "%x (0x", output_d[i], "); "));
                    log_p ("}");
                    log_nl ("")
                )
                [] else -> (
                    skip
                )]
            )
            []  else -> (
                log_st ("");
                log_p ("Scoreboard ", SB_ID, ": TEST FAILED (", num, "); inputs {");
                (; i : IN_CHANNELS : log_p (i, ": ", input[i], "%x (0x", input[i], "); "));
                log_p ("}, outputs {");
                (; i : OUT_CHANNELS : log_p (i, ": expected ", output_m[i], "%x (0x", output_m[i], "), got ", output_d[i], "%x (0x", output_d[i], "); "));
                log_p ("}");
                log_nl ("")
            )];
            num := num + 1
        ]

    }
}

/*
 * Deterministic output only scoreboard
 *
 * Use one instance of this for each end of a deterministic pipeline
 *
 * Features:
 * - Output token comparison
 * - Token counter
 *
 * Assumes:
 * - Same number of tokens on all channels
 * - Tokens on all channels in same order
 *
 * Parameters:
 * - D_WIDTH: Data output bus width
 * - OUT_CHANNELS: Number of output channels coming from the model / design where #tokens is identical
 * - SB_ID: ID of the scoreboard (used in log output)
 * - VERBOSE_TESTING: If false, only failed tests are logged
 *
 */
export template <pint D_WIDTH, OUT_CHANNELS, SB_ID; pbool VERBOSE_TESTING>
defproc deterministic (chan?(int<D_WIDTH>) OUT_M[OUT_CHANNELS], OUT_D[OUT_CHANNELS])
{
    int<D_WIDTH> output_m[OUT_CHANNELS], output_d[OUT_CHANNELS];
    bool not_failed;
    int num;

    chp {

        num := 0;

        *[
            // receive the data
            (, i : OUT_CHANNELS : OUT_M[i]?output_m[i]),
            (, j : OUT_CHANNELS : OUT_D[j]?output_d[j]);

            // check the results
            not_failed+;
            (; i : OUT_CHANNELS : [not_failed -> not_failed := output_m[i] = output_d[i] [] else -> skip]);

            // print the output
            [ not_failed -> (
                
                // only print successful tests if verbose testing is enabled
                [ VERBOSE_TESTING -> (
                    log_st ("");
                    log_p ("Scoreboard ", SB_ID, ": TEST SUCCESS (", num, "); outputs {");
                    (; i : OUT_CHANNELS : log_p (i, ": ", output_d[i], "%x (0x", output_d[i], "); "));
                    log_p ("}");
                    log_nl ("")
                ) 
                []  else -> (
                    skip
                )]
            )
            []  else -> (
                log_st ("");
                log_p ("Scoreboard ", SB_ID, ": TEST FAILED (", num, "); outputs {");
                (; i : OUT_CHANNELS : log_p (i, ": expected ", output_m[i], "%x (0x", output_m[i], "), got ", output_d[i], "%x (0x", output_d[i], "); "));
                log_p ("}");
                log_nl ("")
            )];
            num := num + 1
        ]

    }

}

// external C functions for reordering scoreboard tokens
function add_dut_token (int<64> token) : bool;
function add_model_token (int<64> token) : bool;
function token_fence (int<64> token) : bool;

/*
 * Input token logger
 * 
 * Use this if you are not using a scoreboard with input token logging
 * 
 * Features:
 * 1. Input token logging
 * 2. Token counter
 *
 * Parameters:
 * - D_WIDTH: Data output bus width
 * - IN_CHANNELS: Number of input channels going to the model / design where #tokens is identical
 * - SB_ID: ID of the scoreboard (used in log output)
 * - VERBOSE_TESTING: If false, inputs are not logged
 *
 */
export template <pint D_WIDTH, IN_CHANNELS, SB_ID; pbool VERBOSE_TESTING>
defproc input_logger (chan?(int<D_WIDTH>) IN[IN_CHANNELS])
{
    int<D_WIDTH> input[IN_CHANNELS];
    int num;
    
    chp {

        num := 0;

        *[
            // receive the data
            (, i : IN_CHANNELS : IN[i]?input[i]);

            // only print logged input tokens if verbose testing is enabled
            [ VERBOSE_TESTING -> (
                log_st ("");
                log_p ("Scoreboard ", SB_ID, ": INPUT TOKEN (", num, "); inputs {");
                (; i : IN_CHANNELS : log_p (i, ": ", input[i], "%x (0x", input[i], "); "));
                log_p ("}");
                log_nl ("")
            )
            []  else -> (
                skip
            )];
            num := num + 1
        ]

    }
}

}
}
